plugins {
	id 'java-gradle-plugin'
	id 'groovy'
	id 'maven-publish'
	id 'eclipse'
	id 'com.jfrog.artifactory' version '4.4.15'
}

group 'test'
version '1.0'

dependencies {
	compile 'org.jfrog.buildinfo:build-info-extractor-gradle:4.4.9'
	compile 'org.apache.commons:commons-collections4:4.1'
	testCompile 'junit:junit:4.12'
	testCompile 'org.spockframework:spock-core:1.1-groovy-2.4-rc-3'
}

configurations {
	all*.exclude group: 'org.codehaus.groovy', module: 'groovy-all'
}

gradlePlugin {
	plugins {
		testPlugin {
			id = 'org.mycompany.gradle.testPlugin'
			implementationClass = 'org.mycompany.gradle.TestPlugin'
		}
	}
}

task('dbZip', type: Zip, group: 'Build', description: 'Packages up the DB scripts') {
	classifier = 'db'
	from 'src/main/db'
}

publishing {
	publications {
		mainJava(MavenPublication) {
			artifactId 'some-artifact'
			from components.java
			artifact dbZip
		}
	}
}

artifactoryPublish {
	publications 'mainJava'
}

artifactory {
	contextUrl = 'http://jcenter.bintray.com'
	
	publish {
		repository {
			repoKey = 'mypublishrepo'
			username = 'user'
			password = 'pwd'
		}
		
		defaults {
			properties = ['Deployed By': System.properties['user.name']]
			publishBuildInfo = true
			publishArtifacts = true
			publishPom = true
			publishIvy = false
		}
	}
	
	resolve {
		repository {
			repoKey = '/'
			maven = true
		}
	}
	
	clientConfig.includeEnvVars = true
	clientConfig.envVarsExcludePatterns = '*pwd*,*password*,*PWD*,*PASSWORD*,*secret*,*SECRET*,*key*,*KEY*,sonar.login'
	clientConfig.info.setBuildName('some-artifact')
	clientConfig.info.setBuildNumber(version)
	clientConfig.info.buildUrl = 'build url'
}

tasks.withType(Test) {
	testLogging {
		displayGranularity -1
		minGranularity -1
		maxGranularity -1
		showStandardStreams true
		showStackTraces true
		showExceptions true
		showCauses true
		exceptionFormat "full"
		stackTraceFilters "truncate", "groovy"
		events "passed", "failed", "skipped"
	}

	reports {
		junitXml.outputPerTestCase = true
	}
}