plugins {
	id 'java'
	id 'maven-publish'
	id 'com.jfrog.artifactory' version '4.4.9'
}

group 'test'
version '1.0'

dependencies {
	compile 'org.apache.commons:commons-collections4:4.1'
	testCompile 'junit:junit:4.12'
}

task('dbZip', type: Zip, group: 'Build', description: 'Packages up the DB scripts') {
	classifier = 'db'
	from 'src/main/db'
}

publishing {
	publications {
		mainJava(MavenPublication) {
			artifactId 'some-artifact'
			from project.components.java
			artifact project.tasks['dbZip']
		}
	}
}

artifactoryPublish {
	publications 'mainJava'
}

artifactory {
	contextUrl = 'http://jcenter.bintray.com'
	
	publish {
		repository {
			repoKey = 'mypublishrepo'
			username = 'user'
			password = 'pwd'
		}
		
		defaults {
			properties = ['Deployed By': System.properties['user.name']]
			publishBuildInfo = true
			publishArtifacts = true
			publishPom = true
			publishIvy = false
		}
	}
	
	resolve {
		repository {
			repoKey = '/'
			maven = true
		}
	}
	
	clientConfig.includeEnvVars = true
	clientConfig.envVarsExcludePatterns = '*pwd*,*password*,*PWD*,*PASSWORD*,*secret*,*SECRET*,*key*,*KEY*,sonar.login'
	clientConfig.info.setBuildName('some-artifact')
	clientConfig.info.setBuildNumber(version)
	clientConfig.info.buildUrl = 'build url'
}